#if !( defined(ESP32) ) && !( defined(ESP8266) )
  #error This code is intended to run on ESP8266 or ESP32 platform!
#endif

#include <Arduino.h>
#include <WiFiClient.h>
#include <Time.h>
#include <ezTime.h>
#include <AsyncElegantOTA.h>

#include "wifi/WifiManager.h"
#include "Configuration.h"

#define MAX_CONNECT_TIMEOUT_MS 15000 // 1000 x seconds to connect before creating its own AP
#define BOARD_LED_PIN 2

const int RSSI_MAX =-50;// define maximum straighten of signal in dBm
const int RSSI_MIN =-100;// define minimum strength of signal in dBm

WiFiClient espClient;

int dBmtoPercentage(int dBm) {
  int quality;
  if(dBm <= RSSI_MIN) {
    quality = 0;
  } else if(dBm >= RSSI_MAX) {  
    quality = 100;
  } else {
    quality = 2 * (dBm + 100);
  }
  return quality;
}

const String htmlTop FL_PROGMEM = "<html>\
  <head>\
    <title>%s</title>\
    <style>\
      body { background-color: #303030; font-family: 'Anaheim',sans-serif; Color: #d8d8d8; }\
    </style>\
  </head>\
  <body>\
    <h1>%s LED Controller</h1>";

const String htmlBottom FL_PROGMEM = "<br><br><hr>\
  <p>Uptime: %02d:%02d:%02d | Device time: %s | Device: %s | Current brightness: %0.2f</p>\
  <form method='POST' action='/factory_reset' enctype='application/x-www-form-urlencoded'>\
    <input type='submit' style='font-size: 6pt; color: red; background-color: black;' value='Factory Reset...'>\
  </form>\
  </body>\
</html>";

const String htmlWifiApConnectForm FL_PROGMEM = "<h2>Connect to WiFi Access Point (AP)</h2>\
    <form method='POST' action='/connect' enctype='application/x-www-form-urlencoded'>\
      <label for='ssid'>SSID (AP Name):</label><br>\
      <input type='text' id='ssid' name='ssid'><br><br>\
      <label for='pass'>Password (WPA2):</label><br>\
      <input type='password' id='pass' name='password' minlength='8' autocomplete='off' required><br><br>\
      <input type='submit' value='Connect...'>\
    </form>";

const String htmlDeviceConfigs FL_PROGMEM = "<hr><h2>LED Mode Selector</h2>\
    <form method='POST' action='/led_mode' enctype='application/x-www-form-urlencoded'>\
      <label for='deviceName'>Device name:</label><br>\
      <input type='text' id='deviceName' name='deviceName' value='%s'><br>\
      <br>\
      <label for='frame_delay'>LED strip length:</label><br>\
      <input type='text' id='led_strip_size' name='led_strip_size' value='%i'> LEDs<br>\
      <br>\
      <label for='led_mode'>LED Mode:</label><br>\
      <select name='led_mode' id='led_mode'>\
      %s\
      </select><br>\
      <br>\
      <label for='brightness'>Brightness:</label><br>\
      <input type='text' id='brightness' name='brightness' value='%.2f'> range 0.0-1.0<br>\
      <br>\
      <label for='frame_delay'>Frame delay:</label><br>\
      <input type='text' id='frame_delay' name='frame_delay' value='%i'> milliseconds<br>\
      <br>\
      <label for='cycle_delay'>Auto cycle modes every:</label><br>\
      <input type='text' id='cycle_delay' name='cycle_delay' value='%i'> seconds (0-stay on current mode)<br>\
      <br>\
      <label for='psLedBrightness'>Power-save brightness factor:</label><br>\
      <input type='text' id='psLedBrightness' name='psLedBrightness' value='%.2f'> range 0.0-1.0, multiplied to the default brightness<br>\
      <label for='psStartHour'>Power-save start hour:</label><br>\
      <input type='text' id='psStartHour' name='psStartHour' value='%i'> (0-23)<br>\
      <label for='psEndHour'>Power-save end hour:</label><br>\
      <input type='text' id='psEndHour' name='psEndHour' value='%i'> (0-23)<br>\
      <br>\
      <label for='ntpServer'>NTP Server:</label><br>\
      <input type='text' id='ntpServer' name='ntpServer' value='%s'><br>\
      <label for='gmtOffset_sec'>GMT Offset:</label><br>\
      <input type='text' id='gmtOffset_sec' name='gmtOffset_sec' value='%li'> sec.<br>\
      <label for='daylightOffset_sec'>Daylight Offset:</label><br>\
      <input type='text' id='daylightOffset_sec' name='daylightOffset_sec' value='%i'> sec.<br>\
      <br>\
      <input type='submit' value='Set...'>\
    </form>";

CWifiManager::CWifiManager(): 
rebootNeeded(false), wifiRetries(0) {    
  pinMode(BOARD_LED_PIN,OUTPUT);
  strcpy(SSID, configuration.wifiSsid);
  server = new AsyncWebServer(WEB_SERVER_PORT);
  connect();
}

void CWifiManager::connect() {

  status = WF_CONNECTING;
  strcpy(softAP_SSID, "");
  tMillis = millis();

  if (strlen(SSID)) {

    // Join AP from Config
    Log.infoln("Connecting to WiFi: '%s'", SSID);
    WiFi.begin(SSID, configuration.wifiPassword);
    wifiRetries = 0;
    
  } else {

    // Create AP using fallback and chip ID
    uint32_t chipId = 0;
    #ifdef ESP32
      for(int i=0; i<17; i=i+8) {
        chipId |= ((ESP.getEfuseMac() >> (40 - i)) & 0xff) << i;
      }
    #elif ESP8266
      chipId = ESP.getChipId();
    #endif
  
    Log.infoln("Chip ID: '%i'", chipId);
    sprintf_P(softAP_SSID, "%s_%i", WIFI_FALLBACK_SSID, chipId);
    Log.infoln("Creating WiFi: '%s' / '%s'", softAP_SSID, WIFI_FALLBACK_PASS);
    
    if (WiFi.softAP(softAP_SSID, WIFI_FALLBACK_PASS)) {
      wifiRetries = 0;
      Log.infoln("Wifi AP '%s' created, listening on '%s'", softAP_SSID, WiFi.softAPIP().toString().c_str());
    } else {
      Log.errorln("Wifi AP faliled");
    };

  }
  
}

void CWifiManager::listen() {

  status = WF_LISTENING;

  // Web
  server->on("/", std::bind(&CWifiManager::handleRoot, this, std::placeholders::_1));
  server->on("/connect", HTTP_POST, std::bind(&CWifiManager::handleConnect, this, std::placeholders::_1));
  server->on("/led_mode", HTTP_POST, std::bind(&CWifiManager::handleLedMode, this, std::placeholders::_1));
  server->on("/factory_reset", HTTP_POST, std::bind(&CWifiManager::handleFactoryReset, this, std::placeholders::_1));
  server->begin();
  Log.infoln("Web server listening on %s port %i", WiFi.localIP().toString().c_str(), WEB_SERVER_PORT);

  // OTA
  AsyncElegantOTA.begin(server);

  // NTP
  if (!isApMode()) {
    Log.infoln("Configuring time from %s at %i (%i)", configuration.ntpServer, configuration.gmtOffset_sec, configuration.daylightOffset_sec);
    configTime(configuration.gmtOffset_sec, configuration.daylightOffset_sec, configuration.ntpServer);
    struct tm timeinfo;
    if(getLocalTime(&timeinfo)){
      Log.noticeln("The time is %i:%i", timeinfo.tm_hour,timeinfo.tm_min);
    }
  }
}

void CWifiManager::loop() {

  if (rebootNeeded && millis() - tMillis > 200) {
    Log.noticeln("Rebooting...");
#ifdef ESP32
    ESP.restart();
#elif ESP8266
    ESP.reset();
#endif
    return;
  }

  if (WiFi.status() == WL_CONNECTED || isApMode() ) {
    // WiFi is connected

    if (status != WF_LISTENING) {  
      // Start listening for requests
      listen();
    }

  } else {
    // WiFi is down

    switch (status) {
      case WF_LISTENING: {
        Log.infoln("Disconnecting %i", status);
        server->end();
        status = WF_CONNECTING;
        connect();
      } break;
      case WF_CONNECTING: {
        if (millis() - tMillis > MAX_CONNECT_TIMEOUT_MS) {
          tMillis = millis();
          if (wifiRetries++ > 3) {
            Log.warningln("Connecting failed (wifi status %i) after %l ms, create an AP instead", (millis() - tMillis), WiFi.status());
            strcpy(SSID, "");
          }
          connect();
        }
      } break;

    }

  }
  
}

void CWifiManager::handleRoot(AsyncWebServerRequest *request) {

  Log.infoln("handleRoot");
  
  int sec = millis() / 1000;
  int min = sec / 60;
  int hr = min / 60;

  AsyncResponseStream *response = request->beginResponseStream("text/html");
  response->printf(htmlTop.c_str(), configuration.name, configuration.name);

  if (isApMode()) {
    response->printf(htmlWifiApConnectForm.c_str());
  } else {
    response->printf("<p>Connected to '%s'</p>", SSID);
  }
  
  String modeOptions = "";
  if (modes != NULL) {
    for(uint8_t i=0; i<modes->size(); i++) {
      modeOptions += String("<option") + String(i == configuration.ledMode ? " selected" : "") + String(" value='") + String(i) + String("'>") + (*modes)[i]->getName() + String("</option>");
    }
  }

  response->printf(htmlDeviceConfigs.c_str(), 
    configuration.name, configuration.ledStripSize, 
    modeOptions.c_str(), configuration.ledBrightness, configuration.ledDelayMs, 
    configuration.ledCycleModeMs / 1000,
    configuration.psLedBrightness, configuration.psStartHour, configuration.psEndHour,
    configuration.ntpServer, configuration.gmtOffset_sec, configuration.daylightOffset_sec
  );

  char dTime[100] = "";
  struct tm timeinfo;
  if(!isApMode() && getLocalTime(&timeinfo)) {
    strftime(dTime, 100, "%F %T %z", &timeinfo);
  }

  response->printf(htmlBottom.c_str(), hr, min % 60, sec % 60, dTime, String(DEVICE_NAME), CONFIG_getLedBrightness());
  request->send(response);
}

void CWifiManager::handleConnect(AsyncWebServerRequest *request) {

  Log.info("handleConnect");

  String ssid = request->arg("ssid");
  String password = request->arg("password");
  
  int sec = millis() / 1000;
  int min = sec / 60;
  int hr = min / 60;

  char dTime[100] = "";
  struct tm timeinfo;
  if(!isApMode() && getLocalTime(&timeinfo)) {
    strftime(dTime, 100, "%F %T %z", &timeinfo);
  }

  AsyncResponseStream *response = request->beginResponseStream("text/html");
  response->printf(htmlTop.c_str(), configuration.name, configuration.name);
  response->printf("<p>Connecting to '%s' ... see you on the other side!</p>", ssid.c_str());
  response->printf(htmlBottom.c_str(), hr, min % 60, sec % 60, dTime, String(DEVICE_NAME));
  request->send(response);

  ssid.toCharArray(configuration.wifiSsid, sizeof(configuration.wifiSsid));
  password.toCharArray(configuration.wifiPassword, sizeof(configuration.wifiPassword));

  Log.noticeln("Saved config SSID: '%s'", configuration.wifiSsid);

  EEPROM_saveConfig();

  strcpy(SSID, configuration.wifiSsid);
  connect();

  rebootNeeded = true;
  request->redirect("/");
}

void CWifiManager::handleLedMode(AsyncWebServerRequest *request) {

  Log.info("handleLedMode");

  String deviceName = request->arg("deviceName");
  deviceName.toCharArray(configuration.name, sizeof(configuration.name));
  Log.noticeln("Device req name: %s", deviceName);
  Log.noticeln("Device size %i name: %s", sizeof(configuration.name), configuration.name);

  if (modes != NULL) {
    uint8_t ledMode = atoi(request->arg("led_mode").c_str());
    if (ledMode<modes->size()) {
      configuration.ledMode = ledMode;
    }
  }

  float ledBrightness = atof(request->arg("brightness").c_str());
  if (ledBrightness>=0.0 && ledBrightness<=1.0) {
    configuration.ledBrightness = ledBrightness;
  }

  configuration.ledDelayMs = atol(request->arg("frame_delay").c_str());
  configuration.ledCycleModeMs = atol(request->arg("cycle_delay").c_str()) * 1000;
    
  Log.noticeln("ledMode: '%i'", configuration.ledMode);
  Log.noticeln("ledBrightness: '%D'", configuration.ledBrightness);

  uint16_t ledStripSize = atol(request->arg("led_strip_size").c_str());
  if (configuration.ledStripSize != ledStripSize) {
    Log.noticeln("ledStripSize: '%i'", ledStripSize);
    configuration.ledStripSize = ledStripSize;
    tMillis = millis();
    rebootNeeded = true;
  }

  // NTP

  String ntpServer = request->arg("ntpServer");
  ntpServer.toCharArray(configuration.ntpServer, sizeof(configuration.ntpServer));
  Log.noticeln("ntpServer: %s", ntpServer);

  long gmtOffset_sec = atol(request->arg("gmtOffset_sec").c_str());
  configuration.gmtOffset_sec = gmtOffset_sec;
  Log.noticeln("gmtOffset_sec: %l", gmtOffset_sec);

  int daylightOffset_sec = atoi(request->arg("daylightOffset_sec").c_str());
  configuration.daylightOffset_sec = daylightOffset_sec;
  Log.noticeln("daylightOffset_sec: %i", daylightOffset_sec);

  configTime(configuration.gmtOffset_sec, configuration.daylightOffset_sec, configuration.ntpServer);
  struct tm timeinfo;
  if(getLocalTime(&timeinfo)){
    Log.infoln("The time is %i:%i", timeinfo.tm_hour,timeinfo.tm_min);
  }

  // Power-save

  float psLedBrightness = atof(request->arg("psLedBrightness").c_str());
  if (psLedBrightness>=0.0 && psLedBrightness<=1.0) {
    configuration.psLedBrightness = psLedBrightness;
    Log.noticeln("psLedBrightness: %D", configuration.psLedBrightness);
  }

  int psStartHour = atoi(request->arg("psStartHour").c_str());
  if (psStartHour >= 0 && psStartHour < 23) {  
    configuration.psStartHour = psStartHour;
    Log.noticeln("psStartHour: %i", psStartHour);
  }

  int psEndHour = atoi(request->arg("psEndHour").c_str());
  if (psEndHour >= 0 && psEndHour < 23) {  
    configuration.psEndHour = psEndHour;
    Log.noticeln("psEndHour: %i", psEndHour);
  }

  //

  EEPROM_saveConfig();
  CONFIG_getLedBrightness(true);
  
  request->redirect("/");
}

bool CWifiManager::isApMode() { 
  return WiFi.getMode() == WIFI_AP; 
}

void CWifiManager::handleFactoryReset(AsyncWebServerRequest *request) {
  Log.infoln("handleFactoryReset");
  
  AsyncResponseStream *response = request->beginResponseStream("text/html");
  response->setCode(200);
  response->printf("OK");

  EEPROM_wipe();
  rebootNeeded = true;
  
  request->send(response);
}